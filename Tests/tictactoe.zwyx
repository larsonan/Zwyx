#Plays a perfect game of tic-tac-toe, as X
#Cannot lose, only win or draw

_import:"utilities_lib"


Board~{
        data~bytes.9
        cell~{
                r~int
                c~int
                symbol~{
                        v~int
                        is~{
                                ;~{
                                        p~@CIter:@data
                                        p:{p+c+{r*3}}
                                        t~int:0
                                        t
                                        p.el = v
                                }
                        }
                        set~{
                                ;~{
                                        p~@CIter:@data
                                        p:{p+c+{r*3}}
                                        p.el:v
                                }
                        }
                }
                X~{
                        ~symbol
                        v:"X"
                }
                O~{
                        ~symbol
                        v:"O"
                }
                EMPTY~{
                        ~symbol
                        v:"."
                }
        }
        setup~{
                ;~{
                        p~@CIter:@data
                        c~int:46
                        for.{
                                end:9
                                do:@{
                                        p.el:c
                                        p:{p+1}
                                }  ;
                        }
                }
        }
        printBoard~{
                ;~{
                        p~@CIter:@data
                        for.{
                                end:9
                                do:@{
                                        print.c:p.el
                                        p:{p+1}
                                        {{i % 3} = 2}?{
                                                print.endl
                                        }
                                }  ;
                        }
                }
        }
}

GameState~{
        ~Enum
        DEFAULT~{~op op_val:0}
        TIE~{~op op_val:1}
        WON~{~op op_val:2}
}

Game~{
        board~Board
        state~GameState
        r~int
        c~int
        stateFn~;
        start~;
        center~;
        top_or_bottom~;
        side~;
        opposite~;
        lower_left~;
        upper_right~;
        side_2~;
        top_or_bottom_2~;
        lower_left_2~;
        upper_right_2~;
        opposite_2~;
        center_2~;
        center_3~;
        start:@{
                {{r = 1} & {c = 1}}  ? { r:0 c:1 stateFn:center }
                ^{r = 1}             ? { r:1 c:1 stateFn:side }
                ^{c = 1}             ? { r:1 c:1 stateFn:top_or_bottom }
                ^{{r = 0} & {c = 2}} ? { r:2 c:2 stateFn:upper_right }
                ^{{r = 2} & {c = 0}} ? { r:2 c:2 stateFn:lower_left }
                ^                      { r:2 c:0 stateFn:opposite }
        }
        top_or_bottom:@{
                {{r = 2} & {c = 2}} ? { r:2 c:0 stateFn:top_or_bottom_2 }
                                    ^ { r:2 c:2 state.WON.set }
        }
        top_or_bottom_2:@{
                {c = 0} ? { r:0 c:2 state.WON.set}
                        ^ { r:1 c:0 state.WON.set}
        }
        upper_right:@{
                {{r = 1} & {c = 1}} ? { r:2 c:0 stateFn:upper_right_2 }
                                    ^ { r:1 c:1 state.WON.set }
        }
        upper_right_2:@{
                {c = 0} ? { r:2 c:1 state.WON.set }
                        ^ { r:1 c:0 state.WON.set }
        }
        side:@{
                {{r = 2} & {c = 2}} ? { r:0 c:2 stateFn:side_2 }
                                    ^ { r:2 c:2 state.WON.set }
        }
        side_2:@{
                {r = 0} ? {r:2 c:0 state.WON.set}
                        ^ {r:0 c:1 state.WON.set}
        }
        lower_left:@{
                {{r = 1} & {c = 1}} ? { r:0 c:2 stateFn:lower_left_2 }
                                    ^ { r:1 c:1 state.WON.set }
        }
        lower_left_2:@{
                {r = 0} ? { r:1 c:2 state.WON.set }
                        ^ { r:0 c:1 state.WON.set }
        }
        opposite:@{
                {c = 0} ? { r:0 c:2 stateFn:opposite_2 }
                        ^ { r:1 c:0 state.WON.set }
        }
        opposite_2:@{
                {r = 0} ? { r:1 c:1 state.WON.set }
                        ^ { r:0 c:1 state.WON.set }
        }
        center:@{
                {{r = 0} & {c = 2}} ? { r:2 c:0 stateFn:center_2 }
                                    ^ { r:0 c:2 state.WON.set }
        }
        center_2:@{
                {c = 0} ? { r:1 c:2 stateFn:center_3 }
                        ^ { r:1 c:0 state.WON.set }
        }
        center_3:@{
                {c = 1} ? { r:2 c:2 state.TIE.set }
                        ^ { r:2 c:1 state.TIE.set }
        }
}

;~{
        Game.{
                board.{setup cell.{$.r:0 $.c:0 X.set}}
                state.DEFAULT.set
                stateFn:start
                {state.DEFAULT.is}?*{
                        board.printBoard
                        print:"Enter move (Row,Column):"
                        RInput~int:getChar
                        newChar~int:getChar
                        {newChar = ","}?{
                                newChar:getChar
                        }
                        CInput~int:newChar
                        {newChar != 10}?*{
                                newChar:getChar
                        }
                        {{RInput < "1"} | {RInput > "3"} | {CInput < "1"} | {CInput > "3"}} ? {
                                print.line:"Illegal move."
                        }^{
                                r:{RInput-"1"}
                                c:{CInput-"1"}
                                {board.cell.{r:$.2.r c:$.2.c EMPTY.is}}?{
                                        board.cell.{r:$.2.r c:$.2.c O.set}
                                        stateFn
                                        board.cell.{r:$.2.r c:$.2.c X.set}
                                }^{
                                        print.line:"Illegal move."
                                }
                        }
                }
                board.printBoard
                {state.WON.is}?{
                        print.line:"Game over"
                }^{
                        print.line:"Cats game"
                }
        }
}

