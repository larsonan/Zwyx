#Test for memory management


_import:"utilities_lib"

BoxedInt~{el~int}

MemManagerNF~{
        alloc~{numBytes~int}
        free~{chunk~bytes}
        allocFn~virtual:alloc
        freeFn~virtual:free
}

memCpy~{from~bytes to~bytes ;~{
        fromPtr~@CIter:from
        toPtr~@CIter:to
        num~int:from.count
        {num > 0}?*{
                toPtr.el:fromPtr.el
                fromPtr:{fromPtr+1}
                toPtr:{toPtr+1}
                num:{num-1}
        }
}}

MemArena~{
        ~MemManagerNF
        data~bytes
        allocFn:@{
                {numBytes % 8}?{
                        numBytes:{numBytes / 8 * 8 + 8}
                }
                chunkSize~int:0
                chunkPtr~@BoxedInt:data
                nextChunkPtr~@BoxedInt:chunkPtr
                {{chunkSize < numBytes} & {{chunkPtr.el & 0xFFFF} < 0xFFFF}}?*{
                        {nextChunkPtr.el & 0x8000}?{
                                chunkSize:0
                                chunkPtr:nextChunkPtr
                        }^{
                                {chunkSize > 0}?{
                                        chunkSize:{chunkSize + 8 + nextChunkPtr.el}
                                        chunkPtr.el:chunkSize
                                }^{
                                        chunkPtr:nextChunkPtr
                                        chunkSize:chunkPtr.el
                                }
                        }
                        nextChunkPtr:{nextChunkPtr+8+{nextChunkPtr.el & 0x7FFF}}
                }
                {chunkPtr.el = 0xFFFF}?{0}^{
                        {chunkSize > numBytes}?{
                                nextChunkPtr:{chunkPtr + 8 + numBytes}
                                nextChunkPtr.el:{chunkSize - numBytes - 8}
                        }
                        chunkPtr.el:{numBytes | 0x8000}
                        chunkPtr+8
                }
        }
        freeFn:@{
                chunkPtr~@BoxedInt:{chunk-8}
                chunkPtr.el:{chunkPtr.el & 0x7FFF}
        }
        set~{b~bytes ;~{
                data:b
                
                chunkPtr~@BoxedInt:data
                chunkPtr.el:{data.count-16}
                
                chunkPtr:{chunkPtr + 8}
                for.{i:8 step:8 end:{data.count-8} do:@{
                        chunkPtr.el:0
                        chunkPtr:{chunkPtr+8}
                } ; }
                
                chunkPtr.el:0xFFFF
        }}
}

heapHandlerPageTableBuffer~bytes.400

HeapHandlerPageTableEntry~{
        data~bytes
        spaceLeft~int
}

HeapHandler~{
        ~MemManagerNF
        nPages~int:0
        allocFn:@{
                table~MasterList:HeapHandlerPageTableEntry
                table.buffer:@heapHandlerPageTableBuffer
                
                chunk~bytes
                chunk:0
                
                table.buffer.count:{nPages*24}
                
                n~int:0
                {{chunk = 0} & {n < nPages}}?*{
                        entry~@HeapHandlerPageTableEntry:table.get:n
                        {entry.spaceLeft > numBytes}?{
                                MemArena.{
                                        data:entry.data
                                        chunk:$.alloc:numBytes
                                }
                                entry.spaceLeft:{entry.spaceLeft - numBytes - 8}
                        }
                        n:{n+1}
                }
                
                {chunk = 0}?{
                        table.emplace:@{
                                data:_heap_map:4096
                                data.count:4096
                                spaceLeft:{4080 - numBytes}
                                MemArena.{
                                        set:$.2.data
                                        chunk:$.alloc:numBytes
                                }
                        }
                        n:{n+1}
                        nPages:{nPages+1}
                }
                
                chunkPtr~@BoxedInt:{chunk - 8}
                chunkPtr.el:{chunkPtr.el | {n * 0x10000}}
                
                chunk
        }
        freeFn:@{
                chunkPtr~@BoxedInt:{chunk - 8}
                entry~@HeapHandlerPageTableEntry
                entry:@heapHandlerPageTableBuffer
                entry:{entry + {24 * {chunkPtr.el / 0x10000 - 1}}}
                MemArena.free:chunk
                entry.spaceLeft:{entry.spaceLeft + chunkPtr.el + 8}
        }
}

DynString~{
        b~bytes
        capacity~int:0
        memHandler~@MemManagerNF
        write~{
		~WriteNF
		fn:@{
		        countin~int:data.count
		        failed~int:0
		        
		        {{b.count+countin} > capacity}?{
		                free_b~int:1
		                {capacity = 0}?{
		                        capacity:16
		                        free_b:0
		                        
		                        b.count:0
		                }
		                {{b.count+countin} > capacity}?*{
		                        capacity:{capacity*2}
		                }
		                new_b~bytes:memHandler.alloc:capacity
		                {new_b = 0}?{
		                        failed:1
		                }^{
		                        new_b.count:b.count
		                        memCpy.{to:new_b from:b ;}
		                        {free_b}?{
		                                memHandler.free:b
		                        }
		                        b:new_b
		                }
		        }
		        {failed = 0}?{
			        memCpy.{to:{b+b.count} from:data ;}
			        b.count:{b.count+countin}
			}
		}
	}
	clear~{;~{ b.count:0 }}
	destroy~{;~{
	       clear
	       capacity:0
	       
	       memHandler.free:b
	}}
}

;~{
        mem~HeapHandler
        
        
        str1~DynString.{memHandler:@mem write:"Hello, World!"}
        str2~DynString.{memHandler:@mem write:"Chunk of some data!"}
        print.line:str1
        print.line:str2
        
        str1.write:" Yes, we meet again!"
        str2.write:" Bigger chunk of some data!"
        print.line:str1
        print.line:str2
        
        str1.{destroy write:"Test for finding open chunks."}
        
        entry~@HeapHandlerPageTableEntry
        entry:@heapHandlerPageTableBuffer
        
        buffPtr~@BoxedInt:entry.data
        for.{end:400 do:@{
                print.{d:{buffPtr.el % 256} s:" "}
                buffPtr:{buffPtr+1}
        } ; }
        
        print.endl
        
        for.{end:200 do:@{str1.write:" Filler!"} ; }
        
        table~MasterList:HeapHandlerPageTableEntry
        table.setBuffer:@heapHandlerPageTableBuffer
        entry:table.get:1
        buffPtr:entry.data
        for.{end:400 do:@{
                print.{d:{buffPtr.el % 256} s:" "}
                buffPtr:{buffPtr+1}
        } ; }
        
        print.endl
}

