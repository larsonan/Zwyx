#Miscellaneous tests
#Expected output:
#
#17595CLOSECFFFF1749f0Z
#5040
#1
#0
#BLUE
#ORANGE
#55234
#Result is 6 and 1
#Cannot divide by zero!
#49
#01
#24
#This is in fact a line.
#FGRST
#AGTER

_import:"utilities_lib"

npair~{a~int b~int}

getmessage~{
	write~@WriteNF
	;~{}
}

bm~{
        ;~{p~print getmessage:@p}
}

getmessage.;~{
        write.{c:67 c:76 c:79 c:83 c:69}
}

factorial~{
	in~int
	;~{
		{in < 2} ? 1 ^ {in * factorial:{in-1}}
	}
}

Color~{
	~Enum
	red~{~op op_val:0}
	green~{~op op_val:1}
	orange~{~op op_val:2}
	yellow~{~op op_val:3}
	blue~{~op op_val:4}
}

write_color~{
	write~@WriteNF
	colrw~Color
	;~{
		write.{
			colrw.{
				red.is ? {s:"RED"}
				^ green.is ? {s:"GREEN"}
				^ orange.is ? {s:"ORANGE"}
				^ yellow.is ? {s:"YELLOW"}
				^ blue.is ? {s:"BLUE"}
			}
			endl
		}
	}
}

divmod~{ dd~int dv~int r~int q~int error~String:""
        ;~{
                {dv = 0} ? {
                        error:"Cannot divide by zero!"
                } ^ {
                        q:{dd/dv}
                        r:{dd%dv}
                }
        }
}

divmod_output~{
        a~int
        b~int
        ;~{
                result~divmod.{dd:a dv:b ;}
                {result.error.count > 0} ? {
                        print.line:result.error
                } ^ {
                        print.{s:"Result is " d:result.q s:" and " d:result.r endl}
                }
        }
}

remove_duplicates~{
        l_in~@MasterList:npair
        l_out~@List:npair
        ;~{
                l_in.each:@{
                        outer~$
                        found~int:0
                        l_out.each:@{
                                {{a = outer.a} & {b = outer.b}}?{
                                        found:1
                                }
                        }
                        {found = 0}?{
                                l_out.add:outer
                        }
                }
        }
}

mbuf~bytes.80

;~{
        l~MasterList:npair.{
	        setBuffer:@mbuf
	        emplace.{
		        $:@{a:1 b:0} ;
		        $:@{a:2 b:5} ;
		        $:@{a:0 b:5} ;
		        $:@{a:2 b:7} ;
		        $:@{a:4 b:1} ;
	        }
        }
        l.each:@{print.d:{a+b}}
        
        bm
        print.{
	        hello~bytes.40
	        msg~String
	        msg.setBuffer:@hello
	        msg.write.{getmessage:@$}
	        msg.set.{el:"F" at:1 ; at:2 ; at:3 ; at:4 ;}
	        s:msg
	        d:1749 c:102 d:0 c:90
	        endl
        }
        
        print.{d:factorial:7 endl}
        
        color_a~Color
        color_a.blue.set
        print.{d:color_a.blue.is endl d:color_a.orange.is endl}
        print.{write_color.{write:@$.2 colrw:color_a ; color_a.orange.set colrw:color_a ;}}
        
        n~int:4
        l.each:@{b:n n:{n-1}}
        l.each:@{print.d:{a+b}}
        print.endl
        
        divmod_output.{13,2,; 7,0,;}
        
        l.get.{
                {$:0 ;}.{a:0 b:1}
                {$:1 ;}.{a:2 b:4}
                {$:2 ;}.{a:2 b:4}
                {$:3 ;}.{a:0 b:1}
                {$:4 ;}.{a:2 b:4}
        }
        
        print.d:{6-4/2+5*9}
        print.endl
        
        b1~bytes.48
        
        l2~List:npair
        
        l2.setBuffer:@b1
        
        remove_duplicates.{
                l_in:@l
                l_out:@l2 ;
        }
        
        l2.each:@{print.{d:a d:b endl}}
        
        print.line:"This is in fact a line."
        
        strBuffer1~bytes.40
        strBuffer2~bytes.40
        str1~String.{setBuffer:@strBuffer1 write:"FIRST"}
        str2~String.{setBuffer:@strBuffer2 write:"AFTER"}
        String.set.{at:1 el:71 $.$:@str1 ; $.$:@str2 ;}
        print.{s:str1 endl s:str2 endl}
}

