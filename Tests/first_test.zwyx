_import:"utilities_lib"

npair~{a~int b~int}

getmessage~{
	write~@writenf
	;~{}
}

bm~{
        ;~{p~print getmessage:@p}
}

getmessage.;~{
        write.{c:67 c:76 c:79 c:83 c:69}
}

factorial~{
	in~int
	;~{
		{in < 2} ? 1 ^ {in * factorial:{in-1}}
	}
}

Color~{
	~Enum
	red~{~Op OpVal:0}
	green~{~Op OpVal:1}
	orange~{~Op OpVal:2}
	yellow~{~Op OpVal:3}
	blue~{~Op OpVal:4}
}

write_color~{
	write~@writenf
	colrw~Color
	;~{
		write.{
			colrw.{
				red.Is ? {s:"RED"}
				^ green.Is ? {s:"GREEN"}
				^ orange.Is ? {s:"ORANGE"}
				^ yellow.Is ? {s:"YELLOW"}
				^ blue.Is ? {s:"BLUE"}
			}
			endl
		}
	}
}

divmod~{ dd~int dv~int r~int q~int error~str:""
        ;~{
                {dv = 0} ? {
                        error:"Cannot divide by zero!"
                } ^ {
                        q:{dd/dv}
                        r:{dd%dv}
                }
        }
}

divmod_output~{
        a~int
        b~int
        ;~{
                result~divmod.{dd:a dv:b ;}
                {result.error.count > 0} ? {
                        print.line:result.error
                } ^ {
                        print.{s:"Result is " d:result.q s:" and " d:result.r endl}
                }
        }
}

remove_duplicates~{
        l_in~@MasterList:npair
        l_out~@List:npair
        ;~{
                l_in.each:@{
                        outer~$
                        found~int:0
                        l_out.each:@{
                                {{a = outer.a} & {b = outer.b}}?{
                                        found:1
                                }
                        }
                        {found = 0}?{
                                l_out.add:outer
                        }
                }
        }
}

;~{
        b0~bytes.48
        mbuf~bytes.80
        
        l~MasterList:npair.{
	        setMembBuffer:@mbuf
	        setBuffer:@b0
	        membSize:16
	        emplace.{
		        $:@{a:1 b:0} ;
		        $:@{a:2 b:5} ;
		        $:@{a:0 b:5} ;
		        $:@{a:2 b:7} ;
		        $:@{a:4 b:1} ;
	        }
        }
        l.each:@{print.d:{a+b}}
        
        bm
        print.{
	        hello~bytes.40
	        msg~str
	        msg.set_buffer:@hello
	        msg.write.{getmessage:@$}
	        msg.setchar.{char:"F" at:1 ; at:2 ; at:3 ; at:4 ;}
	        s:msg
	        d:1749 c:102 d:0 c:90
	        endl
        }
        
        print.{d:factorial:7 endl}
        
        color_a~Color
        color_a.blue.Set
        print.{d:color_a.blue.Is endl d:color_a.orange.Is endl}
        print.{write_color.{write:@$.2 colrw:color_a ; color_a.orange.Set colrw:color_a ;}}
        
        n~int:4
        l.each:@{b:n n:{n-1}}
        l.each:@{print.d:{a+b}}
        print.endl
        
        divmod_output.{13,2,; 7,0,;}
        
        l.get.{
                {$:0 ;}.{a:0 b:1}
                {$:1 ;}.{a:2 b:4}
                {$:2 ;}.{a:2 b:4}
                {$:3 ;}.{a:0 b:1}
                {$:4 ;}.{a:2 b:4}
        }
        
        print.d:{6-4/2+5*9}
        print.endl
        
        b1~bytes.48
        
        l2~List:npair
        
        l2.setBuffer:@b1
        
        remove_duplicates.{
                l_in:@l
                l_out:@l2 ;
        }
        
        l2.each:@{print.{d:a d:b endl}}
        
        print.{s:"Almost finished." endl}
        
        strBuffer1~bytes.40
        strBuffer2~bytes.40
        str1~str.{set_buffer:@strBuffer1 write:"FIRST"}
        str2~str.{set_buffer:@strBuffer2 write:"AFTER"}
        str.setchar.{at:1 char:71 $.$:@str1 ; $.$:@str2 ;}
        print.{s:str1 endl s:str2 endl}
}

